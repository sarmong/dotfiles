#!/usr/bin/env zsh

# Solarized colors, taken from http://git.io/solarized-colors.
black=0
blue=33
cyan=37
green=64
orange=166
purple=125
red=124
violet=61
white=15
yellow=136

prompt_git() {
  local s='';
  local branchName='';

  # Check if the current directory is in a Git repository.
  git rev-parse --is-inside-work-tree &>/dev/null || return;

  # Check for what branch we’re on.
  # Get the short symbolic ref. If HEAD isn’t a symbolic ref, get a
  # tracking remote branch or tag. Otherwise, get the
  # short SHA for the latest commit, or give up.
  branchName="$(git symbolic-ref --quiet --short HEAD 2> /dev/null || \
    git describe --all --exact-match HEAD 2> /dev/null || \
    git rev-parse --short HEAD 2> /dev/null || \
    echo '(unknown)')";

  # Early exit for Chromium & Blink repo, as the dirty check takes too long.
  # Thanks, @paulirish!
  # https://github.com/paulirish/dotfiles/blob/dd33151f/.bash_prompt#L110-L123
  repoUrl="$(git config --get remote.origin.url)";
  if grep -q 'chromium/src.git' <<< "${repoUrl}"; then
    s+='*';
  else
    # Check for uncommitted changes in the index.
    if ! $(git diff --quiet --ignore-submodules --cached); then
      s+='+';
    fi;
    # Check for unstaged changes.
    if ! $(git diff-files --quiet --ignore-submodules --); then
      s+='!';
    fi;
    # Check for untracked files.
    if [ -n "$(git ls-files --others --exclude-standard)" ]; then
      s+='?';
    fi;
    # Check for stashed files.
    if $(git rev-parse --verify refs/stash &>/dev/null); then
      s+='$';
    fi;
  fi;

  [ -n "${s}" ] && s=" [${s}]";

  # echo -e "${1}${branchName}${2}${s}";
  GIT_INFO="${1}${branchName}${2}${s}";
}


# set VIMODE according to the current mode (default “[i]”)
insert_icon="%F{67}i"
normal_icon="%F{222}n"
VIMODE="$insert_icon"
function zle-keymap-select {
 VIMODE="${${KEYMAP/vicmd/$normal_icon}/(main|viins)/$insert_icon}"
 zle reset-prompt
}

zle -N zle-keymap-select

## Enable colors
autoload -U colors && colors

setopt prompt_subst

get_node() { NODE_V="%F{$cyan} $(node -v)" }

precmd() {
  prompt_git "%F{$violet}" "%F{$blue}"
  # get_node
}

PROMPT=""
PROMPT+=$'\n'
PROMPT+="%B"
PROMPT+="%F{$orange}%n"
PROMPT+="%F{$white}@"
PROMPT+="%F{$yellow}%M"
PROMPT+="%F{$white} in "
PROMPT+="%F{$green}%~"
PROMPT+='${NODE_V}'
PROMPT+="%F{$white} on "
PROMPT+='${GIT_INFO} '
PROMPT+=$'\n'
PROMPT+='${VIMODE} '
PROMPT+="%F{$white}$%b "
PROMPT+="%{$reset_color%}"

